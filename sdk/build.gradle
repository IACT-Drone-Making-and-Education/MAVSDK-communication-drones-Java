buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath "com.google.protobuf:protobuf-gradle-plugin:0.8.3"
    }
}

subprojects {
    apply plugin: 'com.google.protobuf'
    apply plugin: 'idea'
    apply plugin: 'java-library'

    group = 'io.dronecore'
    version = '0.1.0'

    buildscript {
        repositories {
            jcenter()
        }
    }

    repositories {
        mavenCentral()
    }

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    def grpcVersion = '1.8.0'

    protobuf {
        protoc {
            artifact = 'com.google.protobuf:protoc:3.5.0'
        }

        plugins {
            grpc {
                artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
            }
        }

        generateProtoTasks {
            all()*.plugins {
                grpc {}
            }
        }
    }

    sourceSets {
        main {
            java {
                srcDirs 'build/generated/source/proto/main/grpc'
                srcDirs 'build/generated/source/proto/main/java'
            }
        }
    }

    idea {
        module {
            inheritOutputDirs = false
            outputDir = compileJava.destinationDir
            testOutputDir = compileTestJava.destinationDir
        }
    }

    dependencies {
        implementation "io.grpc:grpc-okhttp:${grpcVersion}"
        implementation "io.grpc:grpc-protobuf:${grpcVersion}"
        implementation "io.grpc:grpc-stub:${grpcVersion}"
    }
}
